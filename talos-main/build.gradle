import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildDate() {
    def df = new SimpleDateFormat("yyyyMMdd")
    return df.format(new Date())
}

def appVersionCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER") ?: 0)

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "org.nargila.robostroke.android.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        if (System.getenv("TRAVIS") == "true") {
            versionCode appVersionCode
            versionName = "Alpha build #" + appVersionCode
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "TalosRowing-alpha-" + appVersionCode + ".apk"
                }
            }
        } else {
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "TalosRowing-${versionName}-${buildDate()}-release.apk"
                }
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':android-common')
    implementation project(path: ':common')

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation "org.slf4j:slf4j-api:${rootProject.ext.slf4jVersion}"

    // https://mvnrepository.com/artifact/log4j/log4j
    implementation "log4j:log4j:1.2.17"

    // https://mvnrepository.com/artifact/ch.acra/acra
    implementation "ch.acra:acra:4.4.0"

    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:4.8.1"
}
